/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // inc
    Inc16(in=outregist, out=outinc);
    Mux16(a=outregist, b=outinc, sel=inc, out=outincrement);

    // load
    Mux16(a=outincrement, b=in, sel=load, out=outload);

    // reset
    Mux16(a=outload, b=false, sel=reset, out=outreset);

    //  Register
    Register(in=outreset, load=true, out=out, out=outregist);
}
